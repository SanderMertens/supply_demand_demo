in package supplychain

enum OperationalState:/
    Off, On, Idle, Error

// Forward declarations
class Product
container Factory
table Factory/Equipment

// Quantity of products
struct ProductQuantity:/
    product: supplychain/Product
    quantity: uint32

class Product:/
    // Static values
    description: string, const
    requires: list{ProductQuantity}, const
    equipment_type: class

class Storage:/
    // Static values
    stores: Product, const

    // Runtime values
    units_stored: uint32

// Equipment component (to be instantiated by subclass of Equipment)
class EquipmentComponent:/
    int16 init()
    int16 validate()

    // Static values
    power_consumption: float32, const|readonly, unit = units/watt

    // Runtime values
    operational_state: supplychain/OperationalState
    temperature: range{float32, {200,150},{450,500}}, readonly, unit=units/kelvin

container Factory:/
    int16 validate()

    // Editable values
    demand: uint32, default = "100000"

    // Static values
    produces: Product, const

    // Runtime values
    power_consumption: float32, readonly, unit=units/watt
    internal_demand: list{ProductQuantity}, readonly

    table Equipment:/
        int16 validate()

        // Static values
        produces: Product, const
        process_steps: list{EquipmentComponent}, const|readonly

        // Runtime values
        power_consumption: float32, readonly, unit=units/watt
        operational_state: supplychain/OperationalState, readonly
        current_step: uint32, private
